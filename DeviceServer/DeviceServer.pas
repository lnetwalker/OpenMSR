program DeviceServer;
{$mode objfpc}
uses PhysMach,webserver,cthreads,classes,crt;

{ $Id$ }

{ This software is copyright (c) 2008 by Hartmut Eilers <hartmut@eilers.net> 	}
{ It is distributed under the terms of the GNU GPL V2 see http://www.gnu.org 	}

{ 20.02.2008		Start of project					}



const
	Forever=false;
	MaxThreads=25;
	BLOCKED=true;
	NOTBLOCKED=false;
	debug=true;
	TimeOut=500;

var
	i		: LongInt;
	ThreadHandle	: array[1..MaxThreads] of LongInt;
	ThreadName	: array[1..MaxThreads] of string;
	shutdown	: Boolean;
	Counter		: LongInt;
	IOGroup		: LongInt;
	ByteValue	: Byte;
	ProtectParams	: TRTLCriticalSection;
	Power		: array [1..8] of byte =(1,2,4,8,16,32,64,128);



function DigitalWriter(p: pointer):LongInt;
begin
	writeln('started Digital Writer Thread..');
	repeat
		PhysMachWriteDigital;
		delay(50);
	until shutdown=true;
	writeln('Digital Writer going down..');

end;


function AnalogReader(p: Pointer):LongInt;
begin
	writeln('started Analog Reader Thread..');
	repeat
		PhysMachReadAnalog;
		delay(100);
	until shutdown=true;
	writeln('Analog Reader going down..');
end;



function DigitalReader(p: pointer):LongInt;
begin
	writeln('started Digital Reader Thread..');
	repeat
		PhysMachReadDigital;
		delay(90);
	until shutdown=true;
	writeln('Digital Reader going down..');
end;



function CounterReader(p: pointer):LongInt;
begin
	writeln('started Counter Reader Thread..');
	repeat
		PhysMachCounter;
		delay(80);
	until shutdown=true;
	writeln('Counter Reader going down..');
end;


				{ Webserver Thread Sarts here }
procedure embeddedWebReadParams;
{ handles any parameters in this case the prameter is always the io_group and maybe a byte_value }
var 
	Url,Params 	: String;
	Trenner		: Byte;

begin
	EnterCriticalSection(ProtectParams);
	Url:=GetURL;

	{ Fragezeichen Abschneiden }
	Params:=copy(GetParams,2,Length(GetParams));;

	if (pos(',',Params) = 0 ) then begin
		val(params,IOGroup);
		ByteValue:=0;
	end
	else begin
		Trenner:=pos(',',Params);
		val(copy(Params,1,Trenner-1),IOGroup);
		val(copy(Params,Trenner+1,Length(Params)-Trenner),ByteValue);
	end;

	LeaveCriticalSection(ProtectParams);

	if debug then begin
		writeln('embeddedWeb:> Got Parameters');
		writeln('URL=',Url,' Parameters=',Params,' ',IOGroup,' ',ByteValue);
	end;
end;



procedure DeliverAnalogValues;
{ called whenever the analog special page is called }
var
	ValueString,SeitenStart,SeitenEnde,Seite,Values	: string;

begin
	inc(Counter);
	SeitenStart:='<html><body>';
	SeitenEnde:=' </body></html>';
	Values:='';

	for i:=1 to 8 do begin
		str(analog_in[i],ValueString);
		Values:=Values+' '+ValueString;
	end;

	Seite:=SeitenStart+Values+SeitenEnde;
	if debug then writeln('embeddedWeb:>Sending Page');

	SendPage(Seite);
	if debug then writeln('embeddedWeb:>Page Send, finished');
end;


procedure DeliverDigitalValues;
{ called whenever the digital special page is called }
var
	SeitenStart,SeitenEnde,Seite,Values	: string;
	AddressBase				: word;
	Bits					: byte;

begin
	inc(Counter);
	SeitenStart:='<html><body>';
	SeitenEnde:=' </body></html>';
	Values:='';

	AddressBase:=IOGroup*8-8+1;

	if debug then writeln('DeliverDigitalValues AddressBAse=',AddressBase);

	for Bits:=1 to 8 do
		if eingang[AddressBase+Bits-1] then Values:=Values+' '+'1'
		else Values:=Values+' '+'0';

	Seite:=SeitenStart+Values+SeitenEnde;

	if debug then writeln('embeddedWeb:>Sending Page');
	SendPage(Seite);

	if debug then writeln('embeddedWeb:>Page Send, finished');
end;


procedure SaveDigitalValues;
var
	SeitenStart,SeitenEnde,Seite,Values	: string;
	AddressBase				: word;
	Bits					: byte;

begin
	inc(Counter);
	SeitenStart:='<html><body>';
	SeitenEnde:=' </body></html>';
	Values:='';

	{ set the bits in the ausgang[n] array in respect of io_group}
	AddressBase:=IOGroup*8-8+1;

	for Bits:=8 downto 1 do begin
		if (ByteValue-Power[Bits])>=0 then begin
			Ausgang[AddressBase+Bits-1]:=true;
			ByteValue:=ByteValue-Power[Bits];
			Values:=Values+' 1';
		end
		else begin
			Ausgang[AddressBase+Bits-1]:=false;
			Values:=Values+' 0';
		end;
	end;

	{ return something usefull }	
	Seite:=SeitenStart+Values+SeitenEnde;
	if debug then writeln('embeddedWeb:>Sending Page');
	SendPage(Seite);
	if debug then writeln('embeddedWeb:>Page Send, finished');
end;



function WebserverThread(p: Pointer):LongInt;
{ the real serving thread }

begin
	writeln('started Webserver Thread, going to start Server...');
	{ start the webserver with IP, Port, Document Root and Logfile }
	start_server('127.0.0.1',10080,BLOCKED,'./docroot/','./pwserver.log');
	writeln;writeln('Webserver started, ready to serve');

	{ register the variable handler }
	SetupVariableHandler(@embeddedWebReadParams);
	
	{ register special URL for content generated by this program }
	SetupSpecialURL('/analog/read.html',@DeliverAnalogValues );
	SetupSpecialURL('/digital/read.html',@DeliverDigitalValues );
	SetupSpecialURL('/digital/write.html',@SaveDigitalValues);

	repeat
		serve_request;
		delay(100);
	until Shutdown=true;

	writeln('Webserver going down..');
	WebserverThread:=0;

end;					{ Webserver Thread end }


begin					{ Main program }
	// initialize Hardware
	PhysMachInit;
	PhysMachloadCfg('DeviceServer.cfg');
	writeln('detected Hardware: ',HWPlatform);
	PhysMachWriteDigital;

	Counter:=0;
	InitCriticalSection(ProtectParams);

	// start threads
	i:=1;
	writeln('starting AnalogReader..');
	ThreadName[i]:='AnalogReader';
	//ThreadHandle[i]:=BeginThread(@AnalogReader,pointer(i));

	inc(i);
	writeln('starting DigitalReader..');
	ThreadName[i]:='DigitalReader';
	ThreadHandle[i]:=BeginThread(@DigitalReader,pointer(i));

	inc(i);
	writeln('starting CounterReader..');
	ThreadName[i]:='CounterReader';
	//ThreadHandle[i]:=BeginThread(@CounterReader,pointer(i));

	inc(i);
	writeln('starting DigitalWriter..');
	ThreadName[i]:='DigitalWriter';
	ThreadHandle[i]:=BeginThread(@DigitalWriter,pointer(i));

	inc(i);
	writeln('Starting Webserver Thread...');
	ThreadName[i]:='Webserver';
	ThreadHandle[i]:=BeginThread(@WebserverThread,pointer(i));

	// fool around and wait for the end
	repeat
		delay(500);
		repeat
		until keypressed;
	until readkey='e';

	// stop threads
	shutdown:=true;

	// wait for threads to finish
	//WaitForThreadTerminate(ThreadHandle[1],TimeOut);
	WaitForThreadTerminate(ThreadHandle[2],TimeOut);
	//WaitForThreadTerminate(ThreadHandle[3],TimeOut);
	WaitForThreadTerminate(ThreadHandle[4],TimeOut);
	WaitForThreadTerminate(ThreadHandle[5],TimeOut);

	DoneCriticalSection(ProtectParams);

end.
